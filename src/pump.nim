import rawk_luigi, rawk_bufferlib
import term, clshell, menubar, config, minimap, gitpane, editor_ref

var
  blinkTicks: int = 0
  gitPollTicks: int = 0
  lastMinimapTop: int = -1
  lastMinimapBuf: pointer = nil
  lastMinimapLines: int = -1
  lastMinimapDirty: int = high(int)
  onTick*: proc() {.closure.} = nil   # assigned by ui for sidebar auto-hide

proc pumpMessage(e: ptr Element, m: Message, di: cint, dp: pointer): cint {.cdecl.} =
  if m == msgAnimate:
    if onTick != nil: onTick()
    clShellDrain()
    drainAll()
    clTickShift(e.window)
    if clShellRunning() and theMenubar != nil:
      elementRepaint(addr theMenubar.e, nil)
    inc gitPollTicks
    if gitPollTicks >= 25:   # ~500ms at 50 Hz
      gitPollTicks = 0
      gitPaneTickPoll()
    inc blinkTicks
    if blinkTicks >= 30:    # ~600ms at 50 Hz
      blinkTicks = 0
      cursorBlinkOn = not cursorBlinkOn
      if theEditor != nil and e.window != nil and
         e.window.focused == (addr theEditor.e) and
         activeMode(theEditor) == cmNormal:
        elementRepaint(addr theEditor.e, nil)
    # Minimap tracks editor scroll, edits, and tab/file switches. Poll the
    # state each tick; repaint only when something actually changed so we
    # don't churn frames during idle. Lines+dirtyFromRow catch in-place
    # text changes (load-file, paste, typed input) where buf ptr doesn't
    # move and topLine often stays at 0.
    if theMinimap != nil and theMinimap.visible and theEditor != nil:
      let buf = activeBuf(theEditor)
      let bp     = cast[pointer](buf)
      let top    = if buf != nil: bufTopLine(buf) else: 0
      let nLines = if buf != nil: bufLines(buf).len else: 0
      let dirty  = if buf != nil: bufDirtyFromRow(buf) else: high(int)
      if bp != lastMinimapBuf or top != lastMinimapTop or
         nLines != lastMinimapLines or dirty != lastMinimapDirty:
        lastMinimapBuf   = bp
        lastMinimapTop   = top
        lastMinimapLines = nLines
        lastMinimapDirty = dirty
        elementRepaint(addr theMinimap.e, nil)
  return 0

proc startPump*(window: ptr Window) =
  let e = elementCreate(csize_t(sizeof(Element)), addr window.e, ELEMENT_HIDE,
                        pumpMessage, "Pump")
  discard elementAnimate(e, false)
