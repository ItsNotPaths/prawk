import std/strutils
import rawk_luigi, rawk_bufferlib
import term, pump, editortabs, menubar, tree, providers, config, resultspane, terminalstack, clshell, project, commands, minimap, gitpane, editor_ref
export rawk_luigi, rawk_bufferlib, menubar

var
  paneEl: ptr Element
  gitPaneEl: ptr Element
  editorEl: ptr Element
  tabPaneEl: ptr Element
  termHostEl: ptr Element
  termStackRef: ptr TerminalStack
  innerSplitRef: ptr SplitPane
  readerMode: bool = false
  savedInnerSplitWeight: cfloat = 0.65
  rootSplitRef: ptr SplitPane
  sidebarEl: ptr Element            # the sidebarSplit element (tree + git)
  sidebarHidden: bool = false       # user's persistent intent (config-backed)
  sidebarPoppedOpen: bool = false   # currently auto-popped from output
  savedRootSplitWeight: cfloat = 0.18

when defined(termDebug):
  proc log(msg: string) =
    try: stderr.writeLine("[prawk] " & msg); stderr.flushFile()
    except IOError: discard
else:
  template log(msg: untyped) = discard

proc isInTermStack(e: ptr Element): bool =
  if termHostEl == nil or e == nil: return false
  var cur = e
  while cur != nil:
    if cur == termHostEl: return true
    cur = cur.parent
  false

proc isInSidebar(e: ptr Element): bool =
  if sidebarEl == nil or e == nil: return false
  var cur = e
  while cur != nil:
    if cur == sidebarEl: return true
    cur = cur.parent
  false

proc columnOf(e: ptr Element): int =
  if e == nil: return -1
  if e == paneEl or e == gitPaneEl: return 0
  if e == editorEl or (tabPaneEl != nil and e == tabPaneEl): return 1
  if isInTermStack(e): return 2
  -1

proc focusElement(target: ptr Element) =
  if target == nil: return
  let win = target.window
  let prev = if win != nil: win.focused else: nil
  elementFocus(target)
  if prev != nil and prev != target:
    elementRepaint(prev, nil)
  elementRepaint(target, nil)

proc focusCol(col: int) =
  case col
  of 0: focusElement(paneEl)
  of 1: focusElement(editorEl)
  of 2:
    let t = stackFocusedTerminal(termStackRef)
    if t != nil: focusElement(addr t.e)
  else: discard

proc onWinMsg(element: ptr Element, message: Message, di: cint, dp: pointer): cint {.cdecl.} =
  if message == msgKeyTyped:
    let k = cast[ptr KeyTyped](dp)
    let w = element.window
    if not w.alt: return 0
    # Shift+Alt+* is reserved for window-level shortcuts (tab cycling,
    # terminal focus prev). Let them through to the shortcut layer.
    if w.shift: return 0

    let code = k.code
    let left  = code == int(KEYCODE_LETTER('H')) or code == int(KEYCODE_LEFT)
    let right = code == int(KEYCODE_LETTER('L')) or code == int(KEYCODE_RIGHT)
    let up    = code == int(KEYCODE_LETTER('K')) or code == int(KEYCODE_UP)
    let down  = code == int(KEYCODE_LETTER('J')) or code == int(KEYCODE_DOWN)
    if not (left or right or up or down): return 0

    let cur = w.focused
    let col = columnOf(cur)

    if left:
      if col == 2: focusCol(1)
      elif col == 1 and not (sidebarHidden and not sidebarPoppedOpen):
        focusCol(0)
    elif right:
      if col == 0: focusCol(1)
      elif col == 1 and not readerMode: focusCol(2)
    elif down:
      if col == 2 and termStackRef != nil and termStackRef.terms.len > 1:
        let nextIdx = termStackRef.focusIdx + 1
        if nextIdx < termStackRef.terms.len:
          stackFocusAt(termStackRef, nextIdx)
      elif col == 0 and cur == paneEl and gitPaneEl != nil:
        focusElement(gitPaneEl)
    elif up:
      if col == 2 and termStackRef != nil and termStackRef.terms.len > 1:
        let prevIdx = termStackRef.focusIdx - 1
        if prevIdx >= 0:
          stackFocusAt(termStackRef, prevIdx)
      elif col == 1 and tabPaneEl != nil:
        focusElement(tabPaneEl)
      elif col == 0 and cur == gitPaneEl and paneEl != nil:
        focusElement(paneEl)
    return 1
  return 0

proc paletteJumpCb(cp: pointer) {.cdecl.} =
  openPaletteWith("jump ")

proc paletteLockCb(cp: pointer) {.cdecl.} =
  ## Prefill the CL with `lock <focused-term-idx>` (1-based, matching the
  ## tN title bars) so Enter toggles the lock on the currently focused
  ## terminal. If no terminals exist, just `lock `.
  var prefix = "lock "
  if termStackRef != nil and termStackRef.terms.len > 0:
    prefix.add($(termStackRef.focusIdx + 1))
  openPaletteWith(prefix)

proc setReaderMode(on: bool) =
  if innerSplitRef == nil or termHostEl == nil: return
  if on == readerMode: return
  readerMode = on
  if on:
    savedInnerSplitWeight = innerSplitRef.weight
    innerSplitRef.weight = 1.0
    termHostEl.flags = termHostEl.flags or ELEMENT_HIDE
    if editorEl != nil:
      let win = editorEl.window
      if win != nil:
        let f = win.focused
        if f != nil and isInTermStack(f):
          focusElement(editorEl)
  else:
    innerSplitRef.weight = savedInnerSplitWeight
    termHostEl.flags = termHostEl.flags and not ELEMENT_HIDE
  elementRefresh(addr innerSplitRef.e)

proc readerModeOn*(): bool = readerMode

proc applySidebarVisibility(visible: bool) =
  ## Drive the actual luigi state: split weight + hide flag. Idempotent —
  ## the visible-bool argument is the desired physical state, independent of
  ## whether the cause was `:zms` or an auto-pop.
  if rootSplitRef == nil or sidebarEl == nil: return
  let currentlyVisible = (sidebarEl.flags and ELEMENT_HIDE) == 0
  if visible == currentlyVisible: return
  if not visible:
    savedRootSplitWeight = rootSplitRef.weight
    rootSplitRef.weight = 0.0
    sidebarEl.flags = sidebarEl.flags or ELEMENT_HIDE
    let win = if sidebarEl.window != nil: sidebarEl.window else: nil
    if win != nil and win.focused != nil and isInSidebar(win.focused) and
       editorEl != nil:
      focusElement(editorEl)
  else:
    rootSplitRef.weight = savedRootSplitWeight
    sidebarEl.flags = sidebarEl.flags and not ELEMENT_HIDE
  elementRefresh(addr rootSplitRef.e)

proc sidebarEnsureVisible*() =
  ## Called by content producers (tree refresh, grep results, shell stream
  ## first-line, etc.). No-op when the sidebar is already on screen.
  if not sidebarHidden: return
  if sidebarPoppedOpen: return    # already popped — leave alone
  sidebarPoppedOpen = true
  applySidebarVisibility(true)

proc sidebarSetHidden*(hidden: bool) =
  ## `:zms` entry point. Sets the persistent intent, clears any auto-popped
  ## state, and updates luigi.
  sidebarHidden = hidden
  sidebarPoppedOpen = false
  applySidebarVisibility(not hidden)

proc sidebarTickCheck() =
  ## Called from pump.onTick at ~50 Hz. When the sidebar is auto-popped
  ## (sidebarHidden + sidebarPoppedOpen), retract it once focus has moved
  ## into the editor or terminal column. CL / menubar focus (col -1) and
  ## sidebar focus (col 0) keep it open.
  if not (sidebarHidden and sidebarPoppedOpen): return
  let e = if sidebarEl != nil and sidebarEl.window != nil: sidebarEl.window.focused
          else: nil
  if e == nil: return
  let col = columnOf(e)
  if col == 1 or col == 2:
    sidebarPoppedOpen = false
    applySidebarVisibility(false)

proc altQDispatch(cp: pointer) {.cdecl.} =
  ## Routes Alt+Q by focused column: editor (body or tab strip) → close active
  ## tab; terminal → kill.
  let win = if editorEl != nil: editorEl.window else: nil
  let f = if win != nil: win.focused else: nil
  if f != nil and (f == editorEl or
                   (tabPaneEl != nil and f == tabPaneEl)):
    discard runCommand("tab.close")
    return
  stackKillFocusedShortcut(cp)

type UiRefs* = object
  window*: ptr Window
  rootPanel*: ptr Panel
  menubar*: ptr Menubar
  rootSplit*: ptr SplitPane
  sidebarSplit*: ptr SplitPane
  innerSplit*: ptr SplitPane
  pane*: ptr ResultsPane
  gitPane*: ptr GitPane
  editorCol*: ptr Panel
  editorTabs*: ptr EditorTabs
  editorBody*: ptr Panel
  editor*: ptr Editor
  minimap*: ptr Minimap
  termStack*: ptr TerminalStack

proc buildUi*(): UiRefs =
  result.window = windowCreate(nil, 0, "prawk", 900, 600)

  result.rootPanel = panelCreate(addr result.window.e, PANEL_GRAY or PANEL_EXPAND)

  result.menubar = menubarCreate(addr result.rootPanel.e, ELEMENT_H_FILL)

  # rootSplit: sidebar | innerSplit
  result.rootSplit = splitPaneCreate(addr result.rootPanel.e,
                                     ELEMENT_V_FILL or ELEMENT_H_FILL, 0.18)

  result.sidebarSplit = splitPaneCreate(addr result.rootSplit.e, SPLIT_PANE_VERTICAL, 0.55)
  result.pane = paneCreate(addr result.sidebarSplit.e)
  result.gitPane = gitPaneCreate(addr result.sidebarSplit.e)
  treeInstall(result.pane)
  providersInstall(result.pane)
  clShellInit(project.projectRoot)
  clShellInstall(result.pane)
  gitPaneInstall()

  # innerSplit: editorCol | terminal-stack
  result.innerSplit = splitPaneCreate(addr result.rootSplit.e, 0, 0.65)

  # editorCol: tab strip on top + editor body underneath, stacked vertically.
  # editorBody runs the editor + minimap horizontally so the minimap can sit
  # on the right edge with a fixed width.
  result.editorCol = panelCreate(addr result.innerSplit.e, PANEL_GRAY or PANEL_EXPAND)
  result.editorTabs = editorTabsCreate(addr result.editorCol.e)
  result.editorBody = panelCreate(addr result.editorCol.e,
                                  PANEL_HORIZONTAL or PANEL_EXPAND or
                                  ELEMENT_V_FILL or ELEMENT_H_FILL)
  let editorHost = EditorHost(
    indentString:    proc(): string         = config.indentString(),
    lineNumbers:     proc(): LineNumberMode = config.lineNumbers,
    cursorMode:      proc(): CursorMode     = config.cursorMode,
    cursorJumpLines: proc(): int            = config.cursorJumpLines,
    recordOpen:      proc(p: string)        = config.pushRecent("recents.files", p),
    onTabsChanged:   proc() =
      if editortabs.theEditorTabs != nil:
        elementRepaint(addr editortabs.theEditorTabs.e, nil))
  result.editor = editorCreate(addr result.editorBody.e,
                               ELEMENT_V_FILL or ELEMENT_H_FILL, editorHost)
  editor_ref.theEditor = result.editor
  result.minimap = minimapCreate(addr result.editorBody.e)
  minimapSetVisible(result.minimap, config.minimapEnabled)

  result.termStack = stackCreate(addr result.innerSplit.e)
  stackInstall(result.termStack)

  let saved = config.readSession()
  let n =
    if saved.len > 0: saved.len
    else: max(1, config.initialTerminals)
  for i in 0 ..< n:
    let nm = if i < saved.len: saved[i] else: ""
    discard stackAddTerminal(result.termStack, nm)

  paneEl       = addr result.pane.e
  gitPaneEl    = addr result.gitPane.e
  editorEl     = addr result.editor.e
  tabPaneEl    = addr result.editorTabs.e
  termHostEl   = addr result.termStack.e
  termStackRef = result.termStack
  innerSplitRef = result.innerSplit
  rootSplitRef = result.rootSplit
  sidebarEl    = addr result.sidebarSplit.e

  # Wire cross-module hooks: content producers ensure-visible, pump tick
  # drives the auto-hide check when the sidebar is in popped state.
  commands.sidebarEnsureVisibleCb = sidebarEnsureVisible
  pump.onTick = sidebarTickCheck

  # Honor persisted hidden state. Toggled later via :zms / :zen-mode-sidebar.
  if not config.sidebarVisible:
    sidebarHidden = true
    applySidebarVisibility(false)

  registerCommand("zen-mode-sidebar", proc(args: seq[string]) =
    # Argument semantics: an explicit on/off describes the *zen mode*, not the
    # sidebar — `on` means hide the sidebar (zen engaged), `off` means show it.
    let hide =
      if args.len >= 1:
        case args[0].toLowerAscii
        of "on", "true", "1", "yes":  true
        of "off", "false", "0", "no": false
        else: sidebarHidden
      else: not sidebarHidden
    sidebarSetHidden(hide)
    config.sidebarVisible = not hide
    config.setConfigKey("sidebar", if hide: "off" else: "on"))
  registerCommand("zms", proc(args: seq[string]) =
    discard runCommand("zen-mode-sidebar", args))

  registerCommand("zen-mode-terminal", proc(args: seq[string]) =
    let on =
      if args.len >= 1:
        case args[0].toLowerAscii
        of "on", "true", "1", "yes":  true
        of "off", "false", "0", "no": false
        else: not readerMode
      else: not readerMode
    setReaderMode(on))
  registerCommand("zmt", proc(args: seq[string]) =
    discard runCommand("zen-mode-terminal", args))

  result.window.e.messageUser = onWinMsg
  log("ui built: pane=" & $cast[uint](paneEl) &
      " editor=" & $cast[uint](editorEl) &
      " termHost=" & $cast[uint](termHostEl) &
      " terms=" & $result.termStack.terms.len)

  let mbCp = cast[pointer](result.menubar)
  let stackCp = cast[pointer](result.termStack)
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('C')), alt: true,
    invoke: paletteOpenCb, cp: mbCp))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('F')), alt: true,
    invoke: openFileMenuCb, cp: mbCp))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('E')), alt: true,
    invoke: openEditMenuCb, cp: mbCp))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('V')), alt: true,
    invoke: openViewMenuCb, cp: mbCp))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('T')), alt: true,
    invoke: stackFocusNext, cp: stackCp))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('T')), alt: true, shift: true,
    invoke: stackFocusPrev, cp: stackCp))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('N')), alt: true,
    invoke: stackNewShortcut, cp: stackCp))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('Q')), alt: true,
    invoke: altQDispatch, cp: stackCp))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('W')), alt: true,
    invoke: paletteJumpCb, cp: nil))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('Z')), alt: true,
    invoke: proc(cp: pointer) {.cdecl.} = editorWrapToggleActive(),
    cp: nil))
  # Shift+Alt+H/L/Left/Right used to be window-wide tab cycle. Dropped —
  # those chords now belong to the editor's word/page motion family. Tabs
  # are still cycled from inside the tab pane (Alt+Up then Left/Right).
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('P')), alt: true, shift: true,
    invoke: paletteLockCb, cp: nil))
  windowRegisterShortcut(result.window, Shortcut(
    code: int(KEYCODE_LETTER('M')), alt: true,
    invoke: proc(cp: pointer) {.cdecl.} = discard runCommand("minimap"),
    cp: nil))

  startPump(result.window)

proc applyInitialFocus*(refs: UiRefs) =
  case config.initialFocus
  of ftTree:
    # Fall back to editor if the sidebar starts hidden — focusing a hidden
    # pane would leave the user stuck with no visible cursor target.
    if sidebarHidden: focusElement(addr refs.editor.e)
    else: focusElement(addr refs.pane.e)
  of ftEditor: focusElement(addr refs.editor.e)
  of ftTerm:
    if refs.termStack != nil and refs.termStack.terms.len > 0:
      stackFocusAt(refs.termStack, config.initialTermIdx)
